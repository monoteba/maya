// -----------------------------------------------------------------------------
// keyMarshal.mel
// -----------------------------------------------------------------------------

// Author: Morten Dalgaard Andersen
// Author URI: http://www.amorten.com
//
// License: Attribution-ShareAlike 4.0 International
// License URI: http://creativecommons.org/licenses/by-sa/4.0/
//
// Version: 1.2
// Modified: 2015-11-03
//
// -----------------------------------------------------------------------------
//
// Instructions on how to use:
//
//  1.	Copy the script to the Maya script folder.
//
//  2.	Execute keyMarshal() from the script editor or add the command to a
// 		shelf button or hotkey.
//
//	3.	Use the supplied icon for the shelf button if you like :)


global int $km_stepsCompleted;

global proc km_progressBegin(int $max, string $status)
{
	// Use the progress bar in the main window
	global string $gMainProgressBar;

	// Figure out how many steps we need to perform
	int $totalSteps				= 3;
	int $optionTickDrawSpecial	= `checkBox -q -v "km_windowTickDrawSpecial"`;
	int $optionKeyAll			= `checkBox -q -v "km_windowKeyAll"`;

	if ($optionTickDrawSpecial)
	{
		$totalSteps = 6;
		if ($optionKeyAll)
			$totalSteps = 7;
	}


	global int $km_stepsCompleted;
	$km_stepsCompleted++;

	string $message = "(" + $km_stepsCompleted + "/" + $totalSteps + ") " + $status;

	if ($max <= 0) {
		$max = 1;
	}

	progressBar -edit
		-beginProgress
		-status $message
		-isInterruptable true
		-maxValue $max
		$gMainProgressBar;
}

global proc km_progressUpdate()
{
	// Use the progress bar in the main window
	global string $gMainProgressBar;
	progressBar -edit -step 1 $gMainProgressBar;
}

global proc km_progressEnd()
{
	// Use the progress bar in the main window
	global string $gMainProgressBar;
	progressBar -edit -endProgress $gMainProgressBar;

	// Remember to disable suspend when done
	refresh -suspend off;
}

global proc float[] km_removeDuplicateKeys(float $listKeys[])
{
	// *** This process is time consuming! ***

	float $result[];
	int $index = 0;

	int $max = size($listKeys);
	km_progressBegin($max, "Processing data...");

	global string $gMainProgressBar;
	for ($item in $listKeys)
	{
		if(`progressBar -q -isCancelled $gMainProgressBar`)
			break;

		if (0 == floatArrayCount($item, $result))
			$result[$index++] = $item;

		km_progressUpdate();
	}

	km_progressEnd();

	// Return array of keyframes
	return $result;
}

global proc float[] km_getKeys(string $objects[], string $timeRange, float $breakdowns[])
{
	// Get a list of all keys
	float $listKeys[];

	if ($timeRange == "")
		$listKeys = `keyframe -q $objects`;
	else
		$listKeys = `keyframe -t $timeRange -q`;

	// Return empty if there are no keys
	if ( !size($listKeys) )
		return {};

	// Remove duplicates
	float $listKeysClean[] = km_removeDuplicateKeys($listKeys);

	// Remove breakdowns
	if ( size($breakdowns) )
	{
		float $results[];
		int $index = 0;
		int $found;

		for ($key in $listKeysClean)
		{
			$found = 0;
			for ($breakdown in $breakdowns)
				if ($breakdown == $key)
					$found = 1;

			if (!$found)
				$results[$index++] = $key;
		}

		// Return keys
		return $results;
	}
	else
	{
		return $listKeysClean;
	}

}

global proc float[] km_getBreakdowns(string $objects[], string $timeRange)
{
	// Get a list of all keys
	float $listKeys[];

	if ($timeRange == "")
		$listKeys = `keyframe -q -breakdown $objects`;
	else
		$listKeys = `keyframe -t $timeRange -q -breakdown $objects`;

	// Return empty if there are no keys
	if ( !size($listKeys) )
		return {};

	// Remove duplicates
	float $results[] = km_removeDuplicateKeys($listKeys);

	// Return keys
	return $results;
}

global proc string[] km_getAttributes(string $objects[])
{
	// Get a list of all animated attributes
	string $attributes[] = `keyframe -q -name $objects`;

	return $attributes;
}

global proc km_adjustCurves(string $attrSet[], float $timeSet[])
{
	if ( !size($attrSet) )
		return;

	int $optionTangents = `checkBox -q -v "km_windowTangents"`;

	if (!$optionTangents)
	{
		// Setup progress bar
		global string $gMainProgressBar;
		int $max = size($attrSet);
		km_progressBegin($max, "Re-adjusting curves...");

		// Set the keys to the default tangent
		int $index = 0;
		string $tangentIn[] = `keyTangent -q -g -inTangentType`;
		string $tangentOut[] = `keyTangent -q -g -outTangentType`;

		for ($attr in $attrSet)
		{
			// Make process interruptable
			if(`progressBar -q -isCancelled $gMainProgressBar`)
				break;

			float $time = $timeSet[$index];
			keyTangent -inTangentType $tangentIn[0] -outTangentType $tangentOut[0] -time $time $attr;
			$index++;

			km_progressUpdate();
		}
	}

	km_progressEnd();
}

global proc km_setFirstKey(string $selection[])
{
	for ($obj in $selection)
	{
		string $attributes[] = `listAnimatable $obj`;
		float $time = `findKeyframe -which "first" $obj`;

		for ($attr in $attributes)
		{
			// Note: May be faster to just set the key, and not test first
			float $keyframe[] = `keyframe -t $time -q -eval $attr`;
			if ( !size($keyframe) )
			{
				float $val = `getAttr $attr`;
				setKeyframe -t $time $attr;
			}
		}
	}
}

// Returns total number of keys set
global proc int km_setKeys(string $attributes[], float $listKeys[], int $breakdown)
{
	if ( !size($listKeys) )
		return 0;

	// Get main progress bar
	global string $gMainProgressBar;

	// Setup progress bar
	int $max = size($listKeys);
	km_progressBegin($max, "Setting new keys...");

	int $optionTangents = `checkBox -q -v "km_windowTangents"`;
	int $index = 0;
	string $listKeysAttrSet[];
	float $listKeysTimeSet[];

	for ($time in $listKeys)
	{
		// Make process interruptable
		if(`progressBar -q -isCancelled $gMainProgressBar`)
			break;

		// Loop through each attribute to determine value
		for ($attr in $attributes)
		{
			// Get value of attribute at certain time
			float $value[] = `keyframe -t $time -q -eval $attr`;
			float $hasKey[] = `keyframe -t $time -q $attr`;

			// Determine whether or not to set a key, so we only set keys where needed!
			if ( !size($hasKey) )
			{
				// Set a key with the value
				setKeyframe -t $time -value $value[0] -breakdown $breakdown -insert $attr;

				if (!$optionTangents)
				{
					// Save a list of the keys that have been set
					$listKeysAttrSet[$index] = $attr;
					$listKeysTimeSet[$index] = $time;
				}
				// Use $index to count how many keys have been set
				$index++;
			}

		}

		// Update progress whenever one key is completed
		km_progressUpdate();
	}

	// End the progress bar
	km_progressEnd();

	// Pass set keys to adjust curves
	if (!$optionTangents)
		km_adjustCurves($listKeysAttrSet, $listKeysTimeSet);

	// Pass attributes to set tick draw special
	int $optionTickDrawSpecial = `checkBox -q -v "km_windowTickDrawSpecial"`;
	if ($optionTickDrawSpecial && !$breakdown)
		km_setTickDrawSpecial($attributes);

	// Return total number of keys set
	return $index;
}

global proc km_setTickDrawSpecial(string $attributes[])
{
	// Step 1: Determine which keys to set
	global string $gMainProgressBar;
	int $max = size($attributes);
	km_progressBegin($max, "Reading tick draw special...");

	int $markIndex = 0;
	float $markedKeys[];

	for ($attr in $attributes)
	{
		int $index = 0;
		string $tdsAttr = $attr + ".kyts";
		float $keys[]	= `keyframe -q $attr`;
		int $tds[];

		// Get list of tick draw special (bit of a hack)
		$tdsIndex = 0;
		for ($key in $keys)
		{
			string $at = $tdsAttr + "[" + $tdsIndex + "]";
			$tds[$tdsIndex] = getAttr($at);
			$tdsIndex++;
		}

		for ($val in $tds)
		{
			if ($val == 1)
			{
				$markedKeys[$markIndex] = $keys[$index];
				$markIndex++;
			}
			$index++;
		}
		km_progressUpdate();
	}

	km_progressEnd();

	// Step 2: Remove duplicates from list
	float $cleanKeys[] = km_removeDuplicateKeys($markedKeys);

	// Step 3: Remove keys out of range if necessary
	float $tdsKeys[];
	int $optionKeyAll = `checkBox -q -v "km_windowKeyAll"`;
	if ($optionKeyAll)
	{
		$tdsKeys = $cleanKeys;
	}
	else
	{
		int $i = 0;
		int $min = `floatField -q -v "km_windowStartTime"`;
		int $max = `floatField -q -v "km_windowEndTime"`;

		int $progressMax = size($cleanKeys);
		km_progressBegin($progressMax, "Limiting range for tick draw special...");
		for ($key in $cleanKeys)
		{
			// add key if within range
			if ($key >= $min && $key <= $max)
				$tdsKeys[$i++] = $key;

			km_progressUpdate();
		}
		km_progressEnd();
	}

	// Step 4: Set tick draw special
	int $max = size($tdsKeys);
	km_progressBegin($max, "Setting tick draw special...");

	for ($key in $tdsKeys)
	{
		for ($attr in $attributes)
		{
			keyframe -t $key -tds true $attr;
		}
		km_progressUpdate();
	}

	km_progressEnd();
}

global proc km_main()
{
	// Get selection
	string $selection[] = `ls -sl`;

	// Warning if selection is empty
	if ( !(size($selection)) )
	{
		warning "No object(s) selected";
		return;
	}

	// Start timer to watch how long it takes! :)
	float $stopwatch = `timerX`;

	// Suspend refresh to speed up process
	refresh -suspend on;

	// Get options
	int $optionKeyAll				= `checkBox -q -v "km_windowKeyAll"`;
	int $optionPerObject			= `checkBox -q -v "km_windowPerObject"`;
	int $optionAllAttributes		= `checkBox -q -v "km_windowAllAttributes"`;
	int $optionTangents				= `checkBox -q -v "km_windowTangents"`;
	int $optionStaticChannels		= `checkBox -q -v "km_windowStaticChannels"`;
	int $optionTickDrawSpecial		= `checkBox -q -v "km_windowTickDrawSpecial"`;

	// Get time range if relevant
	string $timeRange = "";
	if (!$optionKeyAll)
	{
		float $start	= `floatField -q -v "km_windowStartTime"`;
		float $end		= `floatField -q -v "km_windowEndTime"`;
		$timeRange		= $start + ":" + $end;
	}

	// Begin task!
	// If we want to key all attributes, we need to set a key manually on the first key
	if ($optionAllAttributes)
		km_setFirstKey($selection);
	// Delete Static Channels
	else if ($optionStaticChannels)
		delete -staticChannels -unitlessAnimationCurves false -hierarchy none -controlPoints 0 -shape 1;

	int $totalKeysSet;

	// Set steps completed to 0
	global int $km_stepsCompleted;
	$km_stepsCompleted = 0;

	// Determine how to set keys and what attributes are needed
	if ($optionPerObject)
	{
		// Loop through each object separately and set a key
		for ($obj in $selection)
		{
			// Make process interruptable
			global string $gMainProgressBar;
			if(`progressBar -q -isCancelled $gMainProgressBar`)
				break;

			// We need to supply a list of objects to getBreakdowns and getKeys
			// but since we only have one object string, we need to assign to a
			// string array
			string $objects[];
			$objects[0] = $obj;

			// Get lists of breakdowns and keys (breakdowns first, because they're needed to get keys!)
			float $breakdowns[]	= km_getBreakdowns($objects, $timeRange);
			float $keys[]		= km_getKeys($objects, $timeRange, $breakdowns);

			// Get a list of the attributes we want to key
			string $objects[];
			$objects[0] = $obj;
			string $attributes[] = km_getAttributes($objects);

			// Loop and set breakdowns
			$totalKeysSet += km_setKeys($attributes, $breakdowns, true);

			// Loop and set keys
			$totalKeysSet += km_setKeys($attributes, $keys, false);
		}
	}
	else
	{
		// Key all objects wherever there is a key on any object in selection

		// Get lists of breakdowns and keys (breakdowns first, because they're needed to get keys!)
		float $breakdowns[]	= km_getBreakdowns($selection, $timeRange);
		float $keys[]		= km_getKeys($selection, $timeRange, $breakdowns);

		// Get a list of the attributes we want to key
		string $attributes[] = km_getAttributes($selection);

		// Loop and set breakdowns
		$totalKeysSet += km_setKeys($attributes, $breakdowns, true);

		// Loop and set keys
		$totalKeysSet += km_setKeys($attributes, $keys, false);
	}

	// Calculate time the operation took
	float $stopwatchTime = `timerX -startTime $stopwatch`;

	print("// " + $totalKeysSet + " keys have been set in " + $stopwatchTime + " sec.\n");

}

global proc km_closeUI()
{
	if (`window -exists km_windowMain`)
		deleteUI -window km_windowMain;
	// if (`windowPref -exists km_windowMain`)
	// 	windowPref -remove km_windowMain;
}

global proc km_savePref()
{
	if (`window -exists km_windowMain`)
	{
		optionVar -iv "km_windowKeyAll" `checkBox -q -v km_windowKeyAll`;
		optionVar -iv "km_windowPerObject" `checkBox -q -v km_windowPerObject`;
		optionVar -iv "km_windowAllAttributes" `checkBox -q -v km_windowAllAttributes`;
		optionVar -iv "km_windowTangents" `checkBox -q -v km_windowTangents`;
		optionVar -iv "km_windowStaticChannels" `checkBox -q -v km_windowStaticChannels`;
		optionVar -iv "km_windowTickDrawSpecial" `checkBox -q -v km_windowTickDrawSpecial`;
		optionVar -fv "km_windowStartTime" `floatField -q -v km_windowStartTime`;
		optionVar -fv "km_windowEndTime" `floatField -q -v km_windowEndTime`;

		// Used to know if there are any saved prefs beforehand
		optionVar -iv "km_savedPrefs" 1;
	}
}

global proc km_uiToggleTimeRange()
{
	int $enable = !(`checkBox -q -v km_windowKeyAll`);

	text -e -en $enable "km_windowTimeLabel";
	floatField -e -en $enable "km_windowStartTime";
	floatField -e -en $enable "km_windowEndTime";
	button -e -en $enable "km_windowScene";
	button -e -en $enable "km_windowPlayback";
}

global proc km_uiToggleOptions()
{
	int $enable = !(`checkBox -q -v km_windowAllAttributes`);
	checkBox -e -en $enable "km_windowStaticChannels";
}

global proc km_uiSetSceneRange()
{
	float $start	= `playbackOptions -q -ast`;
	float $end		= `playbackOptions -q -aet`;

	floatField -e -v $start "km_windowStartTime";
	floatField -e -v $end "km_windowEndTime";
}

global proc km_uiSetPlaybackRange()
{
	float $start = `playbackOptions -q -min`;
	float $end = `playbackOptions -q -max`;

	floatField -e -v $start "km_windowStartTime";
	floatField -e -v $end "km_windowEndTime";
}

global proc km_loadDefaults()
{
	optionVar -iv "km_savedPrefs" 0;
	km_openUI();
}

global proc km_help()
{
	showHelp -absolute "http://amorten.com/asset/keyMarshal/";
}

global proc km_openUI()
{
	// Delete old UI
	km_closeUI();

	// Get variable values
	int
		$optionKeyAll,
		$optionPerObject,
		$optionAllAttributes,
		$optionTangents,
		$optionStaticChannels,
		$optionTickDrawSpecial;
	float
		$optionStart,
		$optionEnd;

	if (`optionVar -q "km_savedPrefs"`)
	{
		// Load saved values
		$optionKeyAll					= `optionVar -q "km_windowKeyAll"`;
		$optionPerObject				= `optionVar -q "km_windowPerObject"`;
		$optionAllAttributes			= `optionVar -q "km_windowAllAttributes"`;
		$optionTangents					= `optionVar -q "km_windowTangents"`;
		$optionStaticChannels			= `optionVar -q "km_windowStaticChannels"`;
		$optionTickDrawSpecial			= `optionVar -q "km_windowTickDrawSpecial"`;
		$optionStart					= `optionVar -q "km_windowStartTime"`;
		$optionEnd						= `optionVar -q "km_windowEndTime"`;
	}
	else
	{
		// Set default values
		$optionKeyAll					= true;
		$optionPerObject				= false;
		$optionAllAttributes			= false;
		$optionTangents					= false;
		$optionStaticChannels			= false;
		$optionTickDrawSpecial			= false;
		$optionStart					= `playbackOptions -q -ast`;
		$optionEnd						= `playbackOptions -q -aet`;
	}

	// Create window
	int $smallW		= 70;
	int $mediumW	= 100;
	int $largeW		= 137;
	int $height		= 20;

	$km_windowMain = `window -t "keyMarshal 1.2" -sizeable false -w 290 km_windowMain`;

	rowColumnLayout -nr 5 -ro 5 "bottom" 4;

		menuBarLayout -h 20 -w 290 -enableBackground true;
			menu -l "Load Defaults" -en 1;
				menuItem -l "Confirm" -c "km_loadDefaults" -en 1;
				setParent -menu ..;
			setParent..;
			menu -l "Help" -en 1;
				menuItem -l "Online Documentation" -en 1 -c "km_help";
			setParent..;
		setParent..;

		rowLayout;
			separator -style "none" -w 290 -h 1 -bgc 0.2 0.2 0.2;
		setParent..;

		columnLayout -rs 5 -co "both" 4;

			rowColumnLayout -nr 1 -cs 1 5 -ro 1 "top" 4;
				separator -style "none" -width $smallW;
				checkBox -l "Key All" -v $optionKeyAll -height $height -cc "km_uiToggleTimeRange();"
					-ann "Set a key wherever there already is a key."
					km_windowKeyAll;
			setParent..;

			rowColumnLayout -nr 1 -cs 1 5;
				text -l "Start/End" -align "left" -font "boldLabelFont" -width $smallW -height $height km_windowTimeLabel;
				floatField -v $optionStart -minValue -99999999 -maxValue 99999999 -step 1 -pre 2 -w $mediumW -height $height km_windowStartTime;
				floatField -v $optionEnd -minValue -99999999 -maxValue 99999999 -step 1 -pre 2 -w $mediumW -height $height km_windowEndTime;
			setParent..;

			rowColumnLayout -nr 1 -cs 1 5;
				separator -style "none" -width $smallW;
				button -l "Scene" -c "km_uiSetSceneRange();" -w $mediumW -h $height km_windowScene;
				button -l "Playback" -c "km_uiSetPlaybackRange();" -w $mediumW -h $height km_windowPlayback;
			setParent..;

			rowColumnLayout -nr 1 -cs 1 5 -ro 1 "top" 20;
				text -l "Options" -align "left" -width $smallW -height $height -font "boldLabelFont";
				checkBox -l "All Keyable Attributes" -v $optionAllAttributes -height $height -cc "km_uiToggleOptions();"
					-ann "Disable to only set keys in attributes that are already keyed."
					km_windowAllAttributes;
			setParent..;

			rowColumnLayout -nr 1 -cs 1 5;
				separator -style "none" -width $smallW;
				checkBox -l "Per Object Basis" -v $optionPerObject -height $height
					-ann "Set keys based on individual objects.\n\nDisable to set a key wherever there\nis a key from the selection."
					km_windowPerObject;
			setParent..;

			rowColumnLayout -nr 1 -cs 1 5;
				separator -style "none" -width $smallW;
				checkBox -l "Fixed Tangents" -v $optionTangents -height $height
					-ann "Preserve the shape of the animation\ncurves by using fixed tangents.\n\nDisable to use default tangent type."
					km_windowTangents;
			setParent..;

			rowColumnLayout -nr 1 -cs 1 5;
				separator -style "none" -width $smallW;
				checkBox -l "Delete Static Channels" -v $optionStaticChannels -height $height
					-ann "Remove keys on attributes that do not change.\n\nThis speeds up Maya's processing."
					km_windowStaticChannels;
			setParent..;

			rowColumnLayout -nr 1 -cs 1 5 -ro 1 "bottom" 10;
				separator -style "none" -width $smallW;
				checkBox -l "Honour Tick Draw Special" -v $optionTickDrawSpecial -height $height -cc "km_uiToggleOptions();"
					-ann "If any key on a specific frame uses \"draw special\â€,\nall attributes for that frame will become \"draw special\"."
					km_windowTickDrawSpecial;
			setParent..;

		setParent..;

		columnLayout -rs 5 -co "both" 4;

			rowColumnLayout -nr 1 -cs 1 6 -ro 1 "top" 4;
				button -l "Apply" -c "km_apply();" -w $largeW -h 23;
				button -l "Close" -c "km_savePref(); km_closeUI();" -w $largeW -h 23;
			setParent..;

		setParent..;

	setParent..;

	showWindow;

	// Toggle enabled UI elements
	km_uiToggleTimeRange();
	km_uiToggleOptions();
}

global proc km_apply()
{
	km_savePref();
	km_main();
}

global proc keyMarshal()
{
	km_openUI();
}